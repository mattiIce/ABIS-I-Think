$PBExportHeader$f_insert_sheet_skid_package.srf
global type f_insert_sheet_skid_package from function_object
end type

forward prototypes
global function integer f_insert_sheet_skid_package (long al_ab_job_num, long al_sheet_skid_num, n_tr_abc01 atr)
end prototypes

global function integer f_insert_sheet_skid_package (long al_ab_job_num, long al_sheet_skid_num, n_tr_abc01 atr);//Alex Gerlants. 06/15/2018. Arconic_Package_Num. Begin
/*
Function:	f_insert_sheet_skid_package
Returns:		integer	 1 if OK
							-1 if DB error
Arguments:	value	long			al_ab_job_num							
				value	long			al_sheet_skid_num
				value	n_tr_abc01	atr
*/

Integer	li_rtn = 0
Long		ll_package_num, ll_max_value, ll_last_number, ll_value_diff
Boolean	lb_use_package_num

li_rtn = f_get_use_package_num_4job(al_ab_job_num, atr, lb_use_package_num)

If li_rtn = 0 Then //OK in f_get_use_package_num_4job(). li_rtn = sqlca.sqlcode in f_get_use_package_num_4job().
	If IsNull(lb_use_package_num) Then lb_use_package_num = False
Else //DB error in f_get_use_package_num_4job(). Error message is in this function.
	lb_use_package_num = False
End If

If lb_use_package_num Then
	
	li_rtn = f_get_package_num_min_max(atr)

	If li_rtn = -1 Then //Error message is in f_get_package_num_min_max()
		Return li_rtn
	End If
			
	li_rtn = f_get_next_package_num(atr, ll_package_num) //li_rtn = sqlca.sqlcode in f_get_next_package_num()
	
	If li_rtn = 0 Then //OK above
		//Insert into table sheet_skid_package
		insert into dbo.sheet_skid_package values(:al_sheet_skid_num, :ll_package_num) using atr;
		
		If atr.sqlcode <> 0 Then
			//li_rtn = -1
			
			MessageBox("DB Error", "Database error occurred in f_insert_sheet_skid_package while trying to insert row " + &
											"into table sheet_skid_package:" + &
											"~n~rsheet_skid_num = " + String(al_sheet_skid_num) + &
											"~n~rpackage_num    = " + String(ll_package_num) +  + &
											"~n~r~n~rError:~n~r" + atr.sqlerrtext, &
											StopSign!)
		End If
	End If
	
	li_rtn = atr.sqlcode
End If //If lb_use_package_num Then

Return li_rtn
//Alex Gerlants. 06/15/2018. Arconic_Package_Num. End
end function

