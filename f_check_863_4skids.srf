$PBExportHeader$f_check_863_4skids.srf
global type f_check_863_4skids from function_object
end type

forward prototypes
global function integer f_check_863_4skids (long al_ab_job_num, n_tr_abc01 atr)
end prototypes

global function integer f_check_863_4skids (long al_ab_job_num, n_tr_abc01 atr);//Alex Gerlants. 02/25/2019. Print Cert Label. Begin
/*
Function:	f_check_863_4skids
Returns:		integer	 1 if ok
							-1 if DB error
Arguments:	value	long			al_ab_job_num
				value	n_tr_abc01	atr
*/
Integer		li_rtn = 1, li_skid_sheet_status
Long			ll_rows, ll_row, ll_sheet_skid_num, ll_coil_abc_num_prev, ll_coil_abc_num, ll_ab_job_num, ll_i
String		ls_coil_org_num, ls_error_string_email, ls_error_string_popup
Boolean		lb_863_exists_4coil, lb_skid_ok
DataStore	lds_skids_coils_4job, lds_auto_report_emails
String		ls_from_email, ls_from_name, ls_server, ls_port, ls_send_email, ls_send_name, ls_logfile, ls_subject, ls_email_body
String		ls_email_address, ls_email_address_string, ls_coil_cert_label_req
Integer		li_port
Boolean		lb_debugviewer = False
pointer 		lp_oldpointer

lp_oldpointer = SetPointer(HourGlass!)

select distinct upper(nvl(customer.coil_cert_label_req, 'N'))
into	 :ls_coil_cert_label_req
from   ab_job
       join customer_order on customer_order.order_abc_num = ab_job.order_abc_num
       join customer on customer.customer_id = customer_order.orig_customer_id
where  ab_job.ab_job_num = :al_ab_job_num
using	 atr;

If IsNull(ls_coil_cert_label_req) Then ls_coil_cert_label_req = "N"

If ls_coil_cert_label_req <> "Y" Then
	GoTo END_OF_FUNCTION
End If

lds_skids_coils_4job = Create DataStore
lds_skids_coils_4job.DataObject = "d_skids_coils_4job"
lds_skids_coils_4job.SetTransObject(atr)

ll_rows = lds_skids_coils_4job.Retrieve(al_ab_job_num)

Choose Case ll_rows
	Case Is > 0
		//Good
	Case Is < 0
		MessageBox("Database error", 	"Database error occurred in wf_check_863_4skids for w_production" + &
					" while trying to retrieve skids in status 'Ready' for job " + String(al_ab_job_num))
		GoTo END_OF_FUNCTION
	Case 0
		GoTo END_OF_FUNCTION
End Choose

ll_coil_abc_num_prev = 0
lb_skid_ok = True
ls_error_string_email = "Sheet Skid Num~tCoil ABC Num~tCoil Org Num~n~r"

For ll_row = 1 To ll_rows
	ll_ab_job_num = lds_skids_coils_4job.Object.ab_job_num[ll_row]
	ls_coil_org_num = lds_skids_coils_4job.Object.coil_org_num[ll_row]
	ll_sheet_skid_num = lds_skids_coils_4job.Object.sheet_skid_num[ll_row]
	li_skid_sheet_status = lds_skids_coils_4job.Object.skid_sheet_status[ll_row]
	ll_coil_abc_num = lds_skids_coils_4job.Object.coil_abc_num[ll_row]
	
	If ll_coil_abc_num <> ll_coil_abc_num_prev Then
		li_rtn = f_check_863_4coil(ls_coil_org_num, lb_863_exists_4coil, atr) //lb_863_exists_4coil comes back from f_check_863_4coil()
		
		If Not lb_863_exists_4coil Then
			If li_skid_sheet_status = 2 Then //2-Ready
				ls_error_string_popup = ls_error_string_popup + String(ll_coil_abc_num) + " / " + ls_coil_org_num + ", "
			End If
		End If
	End If
	
	If Not lb_863_exists_4coil Then
		If li_skid_sheet_status = 2 Then //2-Ready
			lb_skid_ok = False
	
			//Update skid to status 16-Hold For Cert
			update	sheet_skid
			set		skid_sheet_status = 16
			where		sheet_skid_num = :ll_sheet_skid_num
			using		atr;
			
			If atr.sqlcode < 0 Then //DB error
				li_rtn = -1
				
				MessageBox("Database error", 	"Database error occurred in wf_check_863_4skids for w_production" + &
					" while trying to update skid_sheet_status to 'Hold For Cert' in table sheet_skid for skid " + &
					String(ll_sheet_skid_num) + &
					"~n~r~n~rError:~n~r" + atr.sqlerrtext)
					
				GoTo END_OF_FUNCTION
			End If

			ls_error_string_email = ls_error_string_email + String(ll_sheet_skid_num) + "~t~t" + String(ll_coil_abc_num) + "~t~t" + ls_coil_org_num + "~n~r"		
		End If
	End If
	
	ll_coil_abc_num_prev = ll_coil_abc_num
Next

If Len(ls_error_string_email) > 42 And ls_error_string_popup <> "" Then
	
	//Get SMTP email varaibles from table abis_ini
	ls_from_email = gnv_app.of_GetUserId() + "@albl.com" //Email address "from"
	ls_from_name = gnv_app.of_GetUserId() //Name "from"
	
	ls_server = f_get_ini_value("hold_for_cert_email","smtp_email","server","192.168.3.67") //From table abis_ini
	
	ls_port = f_get_ini_value("hold_for_cert_email","smtp_email","port","25")
	
	If IsNumber(ls_port) Then
		li_port = Integer(ls_port)
	Else
		li_port = 25
	End If
	
	ls_send_email = f_get_ini_value("hold_for_cert_email","smtp_email","SupportEmail","it-support@albl.com")
	ls_send_name = f_get_ini_value("hold_for_cert_email","smtp_email","send_name","Support")
	ls_logfile = f_get_ini_value("hold_for_cert_email","smtp_email","logfile","P:\Email_Errors_LogFiles\SMTP_Email_LogFile.txt")
	ls_subject = f_get_ini_value("hold_for_cert_email","smtp_email","subject","Skids placed on Hold For Cert")
	
	ls_error_string_email =	"The skids for job " + String(ll_ab_job_num) + &
									" from the following coils don't have 863 EDI transmission:~n~r~n~r" + ls_error_string_email
									
	//MessageBox("Test email message", ls_error_string_email) //TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY
	
	//Send email here. **************************************************************************************
	
	//--- Get user emails from table auto_report_emails	
	lds_auto_report_emails = Create DataStore
	lds_auto_report_emails.DataObject = "d_auto_report_emails"
	lds_auto_report_emails.SetTransObject(atr)

	ll_rows = lds_auto_report_emails.Retrieve('hold for cert email', 'all')
	
	If ll_rows < 0 Then
		MessageBox("Database error", 	"Database error occurred in wf_check_863_4skids for w_production" + &
					" while trying to retrieve user emails from table auto_report_emails")
		GoTo END_OF_FUNCTION
	End If
	
	ls_email_body = ls_error_string_email

	For ll_row = 1 To ll_rows
		ls_email_address = lds_auto_report_emails.Object.email_address[ll_row]
		ls_email_address_string = ls_email_address_string + ls_email_address + "~n~r"
		ls_send_email = ls_email_address
		f_send_smtp_email(li_port, ls_email_body, ls_server, ls_logfile, lb_debugviewer, ls_subject, ls_from_email, ls_from_name, ls_send_email, ls_send_name)
	Next

	//---
	
	//Remove the last comma
	ls_error_string_popup = Left(ls_error_string_popup, Len(ls_error_string_popup) - 2)
	ls_error_string_popup = "The following Coil ABC Num / Coil Org Num for job " + String(ll_ab_job_num) + &
									" don't have 863 EDI transmission:~n~r" + ls_error_string_popup
									
	MessageBox("Coils without 863 transmission", ls_error_string_popup + "~n~r~n~r" + &
												"Email sent to the following email addresses:~n~r" + ls_email_address_string)
End If

END_OF_FUNCTION:
SetPointer(lp_oldpointer)
Return li_rtn
//Alex Gerlants. 02/25/2019. Print Cert Label. End
end function

