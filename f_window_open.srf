$PBExportHeader$f_window_open.srf
global type f_window_open from function_object
end type

forward prototypes
global function boolean f_window_open (string as_title)
end prototypes

global function boolean f_window_open (string as_title);//Alex Gerlants. 05/16/2016. Begin
/*
Function:	f_window_open
				Check if sheet with title as_title is already open.
				If it is, return True; and the calling routine will close this sheet.
Return:		boolean	True  if sheet open
							False if sheet not open
Arguments:	value	string	as_title
*/

Boolean	lb_rtn = False
Boolean 	lb_valid
String	ls_title
Integer	li_count = 0
Window 	lw_sheet

lw_sheet = gnv_app.of_getFrame().GetFirstSheet()
lb_valid = IsValid(lw_sheet)

If lb_valid Then
	ls_title = lw_sheet.Title //Get the first sheet title
	
	Do
		lw_sheet = gnv_app.of_getFrame().GetNextSheet(lw_sheet)
		lb_valid = IsValid (lw_sheet)
		
		If lb_valid Then
			ls_title = lw_sheet.Title //Get the next sheet title
			If ls_title = as_title Then //This shipment window is already open 
				li_count++
			End If
		End If
	Loop While lb_valid
	
	//The reason for "> 1" is that at this point from Microsoft Windows' point of view, this new window is already open.
	//Thus, we have 2 of them with the same title open if li_count > 1.
	//In this case, the calling routine will close this window.
	If li_count > 1 Then
		lb_rtn = True
	End If
End If

Return lb_rtn
//Alex Gerlants. 05/16/2016. End
end function

