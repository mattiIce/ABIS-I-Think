$PBExportHeader$f_get_use_package_num_2customer.srf
global type f_get_use_package_num_2customer from function_object
end type

forward prototypes
global function integer f_get_use_package_num_2customer (long al_customer_id, n_tr_abc01 atr, ref boolean ab_use_package_num)
end prototypes

global function integer f_get_use_package_num_2customer (long al_customer_id, n_tr_abc01 atr, ref boolean ab_use_package_num);//Alex Gerlants. 06/15/2018. Arconic_Package_Num. Begin
/*
Function:	f_get_use_package_num_2customer
Returns:		integer	 1 if OK
							-1 if DB error
Arguments:	value			long			al_customer_id
				value			n_tr_abc01	atr
				reference	boolean		ab_use_package_num
*/

Integer	li_rtn = 1
String	ls_use_package_num

select   upper(customer.use_package_num)
into		:ls_use_package_num
from     customer
where    customer.customer_id = :al_customer_id
using		atr;

If atr.sqlcode = 0 Then //OK
	If atr.sqlnrows > 0 Then
		If IsNull(ls_use_package_num) Then ls_use_package_num = "N"
	Else //atr.sqlnrows <= 0
		ls_use_package_num = "N"
	End If
Else //DB error
	li_rtn = -1
	ls_use_package_num = "N"
	
	MessageBox("DB Error", "Database error in f_get_use_package_num_2customer while trying to retrieve use_package_num from table customer" + &
									"~n~r~n~rError:~n~r" + atr.sqlerrtext + &
									"~n~r~n~rAssuming use_package_num = 'N', and continuing processing...", StopSign!)
End If

If ls_use_package_num = "Y" Then 
	ab_use_package_num = True
Else
	ab_use_package_num = False
End If

Return li_rtn
//Alex Gerlants. 06/15/2018. Arconic_Package_Num. End
end function

