$PBExportHeader$f_print_cert_label.srf
global type f_print_cert_label from function_object
end type

forward prototypes
global function integer f_print_cert_label (long al_customer_id, long al_sheet_skid_num, long al_order_abc_num, n_tr_abc01 atr)
end prototypes

global function integer f_print_cert_label (long al_customer_id, long al_sheet_skid_num, long al_order_abc_num, n_tr_abc01 atr);//Alex Gerlants. 09/19/2018. Print cert label. Begin
/*
Function:	f_print_cert_label
Returns:		integer
Arguments:	value	long			al_customer_id
				value	long			al_sheet_skid_num
				value	long			al_order_abc_num
				value	n_tr_abc01	atr
*/

Integer		li_rtn = 1, li_cert_label_customer_code, li_found_row, li_order_item_num
Long			ll_customer_id, ll_des_sh_cust_id, ll_rows, ll_row, ll_rows_print
Long			ll_rows_mech_test_results, ll_i, ll_cert_label_data_elements
Long			ll_863_cert, ll_counter = 0, ll_shipto_customer_id, ll_found_row
Long			ll_order_abc_num
String		ls_cert_label_object, ls_coil_org_num, ls_data_element, ls_column_name
String		ls_mech_test_result, ls_data_element_desc, ls_modstring
String		ls_result_f, ls_result_b, ls_date_f, ls_date_b
String		ls_f_uom, ls_b_uom, ls_cert_label_shipto_name, ls_orig_customer_short_name
String		ls_coil_num_863, ls_display_value, ls_find_string, ls_uom_abbrev_b, ls_uom_abbrev_f
DateTime		ldt_date_f, ldt_date_b
Decimal		ld_result_f, ld_result_b
DataStore	lds_cert_label, lds_coils_on_skid, lds_cert_label_data_elements
DataStore	lds_863_cert, lds_863_mech_test_results, lds_unit_of_measure

Long			ll_ref_order_abc_num //Alex Gerlants. 05/20/2019. Order pack transfer fix
Integer		li_ref_order_abc_item //Alex Gerlants. 05/20/2019. Order pack transfer fix

lds_863_cert = Create DataStore
lds_863_cert.DataObject = "d_863_cert"
lds_863_cert.SetTransObject(atr)

lds_cert_label_data_elements = Create DataStore
lds_cert_label_data_elements.DataObject = "d_cert_label_data_elements"
lds_cert_label_data_elements.SetTransObject(atr)

lds_863_mech_test_results = Create DataStore
lds_863_mech_test_results.DataObject = "d_863_mech_test_results"
lds_863_mech_test_results.SetTransObject(atr)

lds_coils_on_skid = Create DataStore
lds_coils_on_skid.DataObject = "d_coils_on_skid"
lds_coils_on_skid.SetTransObject(atr)

lds_unit_of_measure = Create DataStore
lds_unit_of_measure.DataObject = "d_unit_of_measure"
lds_unit_of_measure.SetTransObject(atr)

//---

////First, check if we have to print cert label
//select	cert_label_shipto_name.cert_label_shipto_name
//into		:ls_cert_label_shipto_name
//from  	sheet_packing_item
//      	join shipment on shipment.packing_list = sheet_packing_item.packing_list
//      	join dbo.cert_label_shipto_name on dbo.cert_label_shipto_name.customer_id = shipment.des_sh_cust_id
//where 	sheet_skid_num = :al_sheet_skid_num
//using		atr;
//
//If atr.sqlcode < 0 Then //DB error
//	Return -1
//End If
//
//Choose Case atr.sqlcode
//	Case 0 //Good
//		//Fall through to print cert label
//	Case 100 //Not found. We should not print cert label for this shipment
//		Return 1
//	Case Else //DB error
//		Return -1
//End Choose

//12/18/2019. As per Jon Fleck (Novelis).
//For now, it is OK to print on cert label customer.customer_short_name instead of printing name from dbo.cert_label_shipto_name
select	dest_customer.customer_short_name, orig_customer.customer_short_name
into		:ls_cert_label_shipto_name, :ls_orig_customer_short_name
from  	sheet_packing_item
      	join shipment on shipment.packing_list = sheet_packing_item.packing_list
      	join customer dest_customer on dest_customer.customer_id = shipment.des_sh_cust_id
        	join customer orig_customer on orig_customer.customer_id = shipment.customer_id
where 	sheet_skid_num = :al_sheet_skid_num
using		atr;

Choose Case atr.sqlcode
	Case 0 //Good
		//Fall through to print cert label
	Case 100 //Not found. Should not happen though
		Return 1
	Case Else //DB error
		MessageBox("DB error", 	"Database error in f_print_cert_label while retrieving customer data." + &
										"~n~r~n~rError:~n~r" + atr.sqlerrtext, &
										StopSign!)
		
		Return -1
End Choose

//---

ll_rows = lds_unit_of_measure.Retrieve()

If ll_rows < 0 Then //DB error
	MessageBox("DB error", 	"Database error in f_print_cert_label while retrieving data from table unit_of_measure.", StopSign!)

	Return -1
End If

select	cert_label_customer_code
into		:li_cert_label_customer_code
from		customer_order
where		order_abc_num = :al_order_abc_num
using		atr;

If atr.sqlcode < 0 Then //DB error
	Return -1
End If

//li_cert_label_customer_code = 1 //TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY

If IsNull(li_cert_label_customer_code) Then 	//cert_label_customer_code not set up.
															//Should not happen because this is checked in f_all_coils_have_863()
	Return 1 //Nothing to do here
End If

ll_cert_label_data_elements = lds_cert_label_data_elements.Retrieve(al_customer_id, li_cert_label_customer_code)

If ll_cert_label_data_elements < 0 Then //DB error
	MessageBox("DB error", 	"Database error in f_print_cert_label while retrieving data from table cert_label_data_elements.", StopSign!)
										
	Return -1
End If

ll_rows = lds_coils_on_skid.Retrieve(al_sheet_skid_num)

If ll_rows < 0 Then //DB error
	MessageBox("DB error", 	"Database error in f_print_cert_label while retrieving data for skid " + String(al_sheet_skid_num), StopSign!)
	
	Return -1
End If

//When a coil from coil table has no corresponding coil in table data_in_863 (coil_org_num), it means that this coil
//doesn't have 863 EDI available.
//In this case, don't print cert labels for this skid al_sheet_skid_num
ls_find_string = "coil_num_863 = 'NA'"
ll_found_row = lds_coils_on_skid.Find(ls_find_string, 1, lds_coils_on_skid.RowCount())

If ll_found_row > 0 Then //This coil doesn't have 863 EDI
	Return 1 //Don't print cert labels for this skid al_sheet_skid_num 
End If


For ll_row = 1 To ll_rows //Coil loop. Begin
		
	ls_coil_org_num = lds_coils_on_skid.Object.coil_org_num[ll_row]
	ll_order_abc_num = lds_coils_on_skid.Object.order_abc_num[ll_row]
	li_order_item_num = lds_coils_on_skid.Object.order_item_num[ll_row]
	
	ll_863_cert = lds_863_cert.Retrieve(al_sheet_skid_num, ls_coil_org_num, ll_order_abc_num, li_order_item_num)
		
	If ll_863_cert < 0 Or ll_rows_mech_test_results > 1 Then //DB error
		If ll_863_cert < 0 Then
			MessageBox("DB error", 	"Database error in f_print_cert_label while retrieving data for skid " + String(al_sheet_skid_num) + " and Coil Org um " + ls_coil_org_num, StopSign!)
		Else
			MessageBox("Error", 	"There are more than 1 row for skid " + String(al_sheet_skid_num) + " and Coil Org um " + ls_coil_org_num, StopSign!)
		End If
		
		Return -1
	//Alex Gerlants. 05/20/2019. Order pack transfer fix. Begin
	ElseIf ll_863_cert = 0 Then
		select	nvl(ref_order_abc_num, 0), nvl(ref_order_abc_item, 0)
		into		:ll_ref_order_abc_num, :li_ref_order_abc_item
		from		sheet_skid
		where		sheet_skid_num = :al_sheet_skid_num
		using		sqlca;
		
		Choose Case sqlca.sqlcode
			Case 0 //Skid found
				If ll_ref_order_abc_num > 0 And li_ref_order_abc_item > 0 Then
					If ll_ref_order_abc_num <> ll_order_abc_num Or li_ref_order_abc_item <> li_order_item_num Then
						//Try to retrieve using ref_order_abc_num and ref_order_abc_item
						ll_863_cert = lds_863_cert.Retrieve(al_sheet_skid_num, ls_coil_org_num, ll_ref_order_abc_num, li_order_item_num)
						
						If ll_863_cert < 0 Or ll_rows_mech_test_results > 1 Then //DB error
							If ll_863_cert < 0 Then
								MessageBox("DB error", 	"Database error in f_print_cert_label while retrieving data for skid " + String(al_sheet_skid_num) + " and Coil Org um " + ls_coil_org_num, StopSign!)
							Else
								MessageBox("Error", 	"There are more than 1 row for skid " + String(al_sheet_skid_num) + " and Coil Org um " + ls_coil_org_num, StopSign!)
							End If
							
							Return -1
						Else 
							//Good. Fall through
						End If
					Else
						Return 1 //Do nothing
					End If
				Else
					Return 1 //Do nothing
				End If
			Case 100 //Skid not found
				Return 1 //Do nothing
			Case Else //DB error
				MessageBox("DB error", 	"Database error in f_print_cert_label while retrieving " + &
												"ref_order_abc_num and ref_order_abc_item for skid " + &
												String(al_sheet_skid_num) + " and Coil Org um " + ls_coil_org_num, StopSign!)
				Return -1
		End Choose
	//Alex Gerlants. 05/20/2019. Order pack transfer fix. End
	End If
	
	ls_modstring = "shipto_customer_name_t" + ".Text = '" + Trim(ls_cert_label_shipto_name) + "'"
	lds_863_cert.Modify(ls_modstring)
	
	ll_rows_mech_test_results = lds_863_mech_test_results.Retrieve(ls_coil_org_num)
	
	If ll_rows_mech_test_results < 0 Or ll_rows_mech_test_results > 1 Then //DB error
		MessageBox("DB error", 	"Database error in f_print_cert_label while retrieving data from table mech_test_results", StopSign!)
		
		Return -1
	End If
	
	//If ls_coil_org_num = '0269944' Then
	//	lds_cert_label_data_elements.Print() //TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY
	//End If
	
	ll_counter = 0 //Initialize
	
	For ll_i = 1 To ll_cert_label_data_elements //Data element loop. Begin
		ls_data_element = lds_cert_label_data_elements.Object.data_element[ll_i]
		ls_data_element_desc = lds_cert_label_data_elements.Object.data_element_desc[ll_i]
		
		ls_column_name = ls_data_element + "_f"
		ls_result_f = lds_863_mech_test_results.GetItemString(1, ls_column_name)
		
		If IsNumber(ls_result_f) Then
			ld_result_f = Dec(ls_result_f)
		Else
			SetNull(ld_result_f)
		End If
		
		ls_column_name = ls_data_element + "_f_date"
		ldt_date_f = lds_863_mech_test_results.GetItemDateTime(1, ls_column_name)
		
		ls_column_name = ls_data_element + "_f_uom"
		ls_f_uom = lds_863_mech_test_results.GetItemString(1, ls_column_name)
		
		ls_find_string = "lower(uom_code) = lower('" + ls_f_uom + "')"
		li_found_row = lds_unit_of_measure.Find(ls_find_string, 1, lds_unit_of_measure.RowCount())
		
		If li_found_row > 0 Then
			ls_uom_abbrev_f = lds_unit_of_measure.Object.uom_abbrev[li_found_row]
		Else
			ls_uom_abbrev_f = ""
		End If
		If IsNull(ls_uom_abbrev_f) Then ls_uom_abbrev_f = ""
		
		
		
		ls_column_name = ls_data_element + "_b"
		ls_result_b = lds_863_mech_test_results.GetItemString(1, ls_column_name)
		
		If IsNumber(ls_result_b) Then
			ld_result_b = Dec(ls_result_b)
		Else
			SetNull(ld_result_b)
		End If
		
		ls_column_name = ls_data_element + "_b_date"
		ldt_date_b = lds_863_mech_test_results.GetItemDateTime(1, ls_column_name)
		
		ls_column_name = ls_data_element + "_b_uom"
		ls_b_uom = lds_863_mech_test_results.GetItemString(1, ls_column_name)
		
		ls_find_string = "lower(uom_code) = lower('" + ls_b_uom + "')"
		li_found_row = lds_unit_of_measure.Find(ls_find_string, 1, lds_unit_of_measure.RowCount())
		
		If li_found_row > 0 Then
			ls_uom_abbrev_b = lds_unit_of_measure.Object.uom_abbrev[li_found_row]
		Else
			ls_uom_abbrev_b = ""
		End If
		
		If IsNull(ls_uom_abbrev_b) Then ls_uom_abbrev_b = ""
		
		
		
		If IsNull(ld_result_f) And IsNull(ld_result_b) Then //Both values not populated
			Continue //Go to next iteration
		Else
			ll_counter++
			
			If Not IsNull(ld_result_f) And Not IsNull(ld_result_b) Then //Both values populated
				If ld_result_f = ld_result_b Then
					ls_display_value = ls_result_f + " " + ls_uom_abbrev_f
				Else //ld_result_f and ld_result_b not the same
					//Compare dates
					If ldt_date_f = ldt_date_b Then
						ls_display_value = ls_result_f + " " + ls_uom_abbrev_f //Report OD only
					Else //ldt_date_f <> ldt_date_b
						If ldt_date_f > ldt_date_b Then
							ls_display_value = ls_result_f + " " + ls_uom_abbrev_f
						Else //ldt_date_f < ldt_date_b
							ls_display_value = ls_result_b + " " + ls_uom_abbrev_f
						End If
					End If
				End If
			Else //Either ld_result_f is Null or ld_result_b is Null
				If Not IsNull(ld_result_f) Then
					ls_display_value = ls_result_f + " " + ls_uom_abbrev_f
				Else //Not If IsNull(ld_result_b)
					ls_display_value = ls_result_b + " " + ls_uom_abbrev_b
				End If
			End if
		End If
		
		//---

		ls_modstring = "desc_t" + String(ll_counter) + ".Text = '" + ls_data_element_desc + "'"
		lds_863_cert.Modify(ls_modstring)
		
		ls_modstring = "data_t" + String(ll_counter) + ".Text = '" + ls_display_value + "'"
		lds_863_cert.Modify(ls_modstring)
	Next //Data element loop. End
	
	If ll_counter > 0 Then
		//lds_863_cert.Object.coil_org_num[1] = '1721209' //TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY TEST ONLY
		lds_863_cert.Print()
	Else //None of test results populated. We have to notify Novelis. They have to fix it in the future
		MessageBox("Cannot print cert label", 	"Cannot print cert label for customer coil number " + ls_coil_org_num + &
															"~n~rNone of test results in 863 EDI transmission populated for this coil." + &
															"~n~r~n~rPlease notify customer " + ls_orig_customer_short_name, StopSign!)
	End If
Next //Coil loop. End

Return li_rtn
//Alex Gerlants. 09/19/2018. Print cert label. End
end function

