$PBExportHeader$f_all_coils_have_863.srf
global type f_all_coils_have_863 from function_object
end type

forward prototypes
global function any f_all_coils_have_863 (long al_packing_list, n_tr_abc01 atr)
end prototypes

global function any f_all_coils_have_863 (long al_packing_list, n_tr_abc01 atr);//Alex Gerlants. 09/19/2018. Print cert label. Begin
/*
Function:	f_all_coils_have_863
Returns:		integer
Arguments:	value			long			al_packing_list
				value			n_tr_abc01	atr
*/

Integer		li_rtn = 1, li_cert_label_customer_code, li_counter, li_answer
Long			ll_order_abc_num, ll_customer_id, ll_rows, ll_row
String		ls_customer_short_name, ls_cert_label_object, ls_customer_name
String		ls_coil_org_num, ls_err_string
String		ls_err_string_orders, ls_err_string_coils
DataStore	lds_coils_in_packing_list, lds_all_coils_have_863
dwbuffer		l_dwbuffer

lds_all_coils_have_863 = Create DataStore
lds_all_coils_have_863.DataObject = "d_all_coils_have_863"
lds_all_coils_have_863.SetTransObject(atr)

lds_coils_in_packing_list = Create DataStore
lds_coils_in_packing_list.DataObject = "d_coils_in_packing_list"
lds_coils_in_packing_list.SetTransObject(atr)

ll_rows = lds_all_coils_have_863.Retrieve(al_packing_list)

//lds_all_coils_have_863 selects customer orders for al_packing_list that don't have customer_order.cert_label_customer_code set up.
//If ll_rows = 0, this is good. It means that all customer orders for al_packing_list have customer_order.cert_label_customer_code set up.
//This is good...fall through
Choose Case ll_rows
	Case 0 //Good
		//
	Case Is > 0 //1 or more orders don't have customer_order.cert_label_customer_code set up
		If ll_rows = 1 Then
			ll_order_abc_num = lds_all_coils_have_863.Object.order_abc_num[ll_rows]
			ls_err_string_orders = String(ll_order_abc_num)
		Else //ll_rows > 1
			For ll_row = 1 To ll_rows
				ll_order_abc_num = lds_all_coils_have_863.Object.order_abc_num[ll_row]
				ls_err_string_orders = ls_err_string_orders + String(ll_order_abc_num) + ", "
			Next
			
			//Remove the last comma
			ls_err_string_orders = Left(ls_err_string_orders, Len(ls_err_string_orders) - 2)
		End If
	Case Is < 0
		li_rtn = -1
		MessageBox("DB error", 	"Database error in f_all_coils_have_863 while retrieving customer_order.cert_label_customer_code." + &
										" for packing list " + String(al_packing_list), StopSign!)
		GoTo CONT
End Choose

ll_rows = lds_coils_in_packing_list.Retrieve(al_packing_list)

If ll_rows < 0 Then //DB error
	li_rtn = -1
	MessageBox("DB error", 	"Database error in f_all_coils_have_863 while retrieving coil information" + &
										" for packing list " + String(al_packing_list), StopSign!)
	GoTo CONT
End If
	
For ll_row = 1 To ll_rows
	ls_coil_org_num = lds_coils_in_packing_list.Object.coil_org_num[ll_row]
	
	select	count(*)
	into		:li_counter
	from		dbo.data_in_863
	where		coil_num = :ls_coil_org_num
	using		atr;
	
	If atr.sqlcode = 0 Then
		If li_counter <= 0 Then
			ls_err_string = ls_err_string + ls_coil_org_num + ", "
		End If
	Else //DB error
		li_rtn = -1
		MessageBox("DB error", 	"Database error in f_all_coils_have_863 while retrieving coil 863 information" + &
											" for coil org number " + ls_coil_org_num + &
											"~n~r~n~rError:~n~r" + atr.sqlerrtext, StopSign!)
		GoTo CONT
	End If
Next

If ls_err_string <> "" Then
	//Remove the last comma
	ls_err_string = Left(ls_err_string, Len(ls_err_string) - 2)
End If

If ls_err_string_orders <> "" Or ls_err_string  <> "" Then
	If ls_err_string_orders <> "" And ls_err_string  <> "" Then
		li_answer = MessageBox("Cannot print Cert Labels for Packing List " + String(al_packing_list), &
									"Cert Label Customer Code is NOT set up for Customer orders " + ls_err_string_orders + ".~n~r" + &
									"Please set it up on Order Entry screen, ~n~rCert Label Customer Code drop-down.~n~r~n~r" + &
									"One or more coils in this shipment have no cert label EDI information~n~r" + &
									"The following customer coil numbers in this shipment " + String(al_packing_list) + &
									" have not received EDI information to print cert labels:" + &
									"~n~r" + ls_err_string + &
									"~n~rCert labels will not print for these coils." + &
									"~n~r~n~rDo you still want to print barcodes for this shipment " + String(al_packing_list) + "?", &
									Question!, YesNo!, 2)
	ElseIf ls_err_string_orders <> "" Then
		li_answer = MessageBox("Cannot print Cert Labels for Packing List " + String(al_packing_list), &
									"Cert Label Customer Code is NOT set up for Customer orders " + ls_err_string_orders + ".~n~r" + &
									"Please set it up on Order Entry screen, ~n~rCert Label Customer Code drop-down." + &
									"~n~r~n~rDo you still want to print barcodes for this shipment " + String(al_packing_list) + "?", &
									Question!, YesNo!, 2)
	Else //ls_err_string  <> ""
		li_answer = MessageBox("One or more coils in this shipment have no cert label EDI information", &
									"The following customer coil numbers in this shipment " + String(al_packing_list) + &
									" have not received EDI information to print cert labels:" + &
									"~n~r" + ls_err_string + &
									"~n~rCert labels will not print for these coils." + &
									"~n~r~n~rDo you still want to print barcodes for this shipment " + String(al_packing_list) + "?", &
									Question!, YesNo!, 2)
	End If
	
	If li_answer = 2 Then //No
		li_rtn = 2
	End If
End If

CONT:
Return li_rtn
//Alex Gerlants. 09/19/2018. Print cert label. End
end function

