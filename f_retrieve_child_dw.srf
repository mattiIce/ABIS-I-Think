$PBExportHeader$f_retrieve_child_dw.srf
global type f_retrieve_child_dw from function_object
end type

forward prototypes
global function integer f_retrieve_child_dw (ref datawindow adw_parent_dw, ref datawindowchild adw_child_dw, string as_parent_dw_column_name, string as_child_dw_column_name, string as_child_dw_sql_orig, string as_child_dw_add_2sql_string, n_tr_abc01 atr_trans, boolean ab_remove_duplicates, boolean ab_insert_all)
end prototypes

global function integer f_retrieve_child_dw (ref datawindow adw_parent_dw, ref datawindowchild adw_child_dw, string as_parent_dw_column_name, string as_child_dw_column_name, string as_child_dw_sql_orig, string as_child_dw_add_2sql_string, n_tr_abc01 atr_trans, boolean ab_remove_duplicates, boolean ab_insert_all);//Alex Gerlants. Customer_Shipment_Rpt_All_Together. Begin
/*
Function:	f_retrieve_child_dw
Returns:		integer
Arguments:	reference	datawindow			adw_parent_dw
				reference	datawindowchild	adw_child_dw
				value			string				as_parent_dw_column_name
				value			string				as_child_dw_column_name
				value			string				as_child_dw_sql_orig
				value			string				as_child_dw_add_2sql_string
				value			n_tr_abc01			atr_trans
				value			boolean				ab_remove_duplicates	<== Remove duplicate values from adw_child_dw
				value			boolean				ab_insert_all			<== Insert "ALL" into first row of adw_parent_dw and adw_child_dw
*/

Long					ll_rows, ll_row, ll_row_inserted
Integer				li_rtn
String				ls_sql_new, ls_value_prev, ls_value
DataWindowChild	ldwc

li_rtn = adw_parent_dw.GetChild(as_parent_dw_column_name, ldwc)

If li_rtn = 1 Then //OK
	ldwc.SetTransObject(atr_trans)
	
	ls_sql_new = as_child_dw_sql_orig + as_child_dw_add_2sql_string
	li_rtn = ldwc.SetSqlSelect(ls_sql_new)
	
	If li_rtn = 1 Then //OK
		//ldwc.SetTransObject(atr_trans)
		ll_rows = ldwc.Retrieve()
		
		If ll_rows > 0 Then
			If ab_remove_duplicates Then
				//Remove duplicates
				ls_value_prev = ""
				li_rtn = ldwc.SetSort(as_child_dw_column_name)
				li_rtn = ldwc.Sort()
				
				For ll_row = ll_rows To 1 Step -1
					ls_value = Trim(ldwc.GetItemString(ll_row, as_child_dw_column_name))
					
					If ls_value = ls_value_prev Then
						ldwc.DeleteRow(ll_row)
					End If
					
					ls_value_prev = ls_value
				Next
			End If
			
			If ab_insert_all Then
				//Insert "ALL" into first row of adw_parent_dw and adw_child_dw
				ll_row_inserted = ldwc.InsertRow(1)
				
				If ll_row_inserted > 0 Then
					ldwc.SetItem(ll_row_inserted, as_child_dw_column_name, "ALL")
					adw_parent_dw.SetItem(ll_row_inserted, as_parent_dw_column_name, "ALL")
				End If
			End If
		End If
	End If
End If

Return li_rtn
//Alex Gerlants. Customer_Shipment_Rpt_All_Together. End
end function

